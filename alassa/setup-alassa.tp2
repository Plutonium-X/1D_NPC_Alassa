BACKUP ~alassa\backup~
AUTHOR ~SimDing0@hotmail.com~

VERSION ~v3~

ALWAYS
 ACTION_DEFINE_ARRAY tra_reload BEGIN setup END
 LAF HANDLE_CHARSETS
 INT_VAR
 infer_charset = 1
 STR_VAR
 tra_path = EVAL ~%MOD_FOLDER%\tra~
 reload_array = tra_reload
 END
 END

AUTO_TRA ~alassa\tra\%s~

LANGUAGE ~English~
         ~English~
         ~alassa\tra\English\Setup.tra~

LANGUAGE ~Deutsch (Uebersetzung von Thioderik)~
         ~German~
         ~alassa\tra\German\Setup.tra~

LANGUAGE ~French (by Shaywen and Zooloo, of the d'Oghmatiques)~
         ~French~
         ~alassa\tra\French\Setup.tra~

BEGIN @0

REQUIRE_FILE ~data/25dialog.bif~ @1

ACTION_IF GAME_IS ~TOB BGT~ THEN BEGIN
COPY ~alassa/portraits~ ~override~
END
ACTION_IF GAME_IS ~BG2EE EET~ THEN BEGIN
COPY ~alassa/portraitsEE~ ~override~
END

APPEND ~pdialog.2da~
~D0Alassa     D0AlasP            D0AlasJ           D0AlasD        D0Ala25P           D0Ala25J              D0Ala25D              D0Alas25~
UNLESS ~D0Alassa~

APPEND ~interdia.2da~
~D0Alassa    BD0Alas       BD0Ala25~
UNLESS ~D0Alassa~

COPY ~alassa/2da/d0aland1.2da~ ~override~
REPLACE ~99999~ @2

COPY ~alassa/2da/d0aland2.2da~ ~override~
REPLACE ~99999~ @3

COPY ~alassa/creatures/d0alassa.cre~ ~override~
     ~alassa/creatures/d0alas25.cre~ ~override~
SAY NAME1 @4
SAY NAME2 @4
SAY INITIAL_MEETING @5 
SAY MORALE @6 
SAY HAPPY @7 
SAY UNHAPPY_ANNOYED @8 
SAY UNHAPPY_SERIOUS @9 
SAY UNHAPPY_BREAKING @10 
SAY LEADER @11 
SAY TIRED @12 
SAY BORED @13 
SAY BATTLE_CRY1 @14 
SAY BATTLE_CRY2 @15 
SAY BATTLE_CRY3 @16 
SAY BATTLE_CRY4 @17 
SAY BATTLE_CRY5 @18 
SAY DAMAGE ~~ [d0alas14]
SAY DYING ~~ [d0alas15]
SAY HURT @21 
SAY AREA_FOREST @22 
SAY AREA_CITY @23 
SAY AREA_DUNGEON @24 
SAY AREA_DAY @25 
SAY AREA_NIGHT @26 
SAY SELECT_COMMON1 @27 
SAY SELECT_COMMON2 @28 
SAY SELECT_COMMON3 @29 
SAY SELECT_COMMON4 @30 
SAY SELECT_COMMON5 @31 
SAY SELECT_COMMON6 @32 
SAY SELECT_ACTION1 @33 
SAY SELECT_ACTION2 @34 
SAY SELECT_ACTION3 @35 
SAY SELECT_ACTION4 @36 
SAY SELECT_ACTION5 @37 
SAY SELECT_ACTION6 @38 
SAY SELECT_ACTION7 @39 
SAY SELECT_RARE1 @40 
SAY SELECT_RARE2 @41 
SAY CRITICAL_HIT @42 
SAY CRITICAL_MISS @43 
SAY TARGET_IMMUNE @44 
SAY INVENTORY_FULL @45 
SAY PICKED_POCKET @46 
SAY HIDDEN_IN_SHADOWS @47 
SAY SET_A_TRAP @48 
SAY BIO @49

EXTEND_TOP ~ar0400.bcs~ ~alassa/scripts/et_0400.baf~

EXTEND_TOP ~ar6200.bcs~ ~alassa/scripts/et_6200.baf~

COMPILE ~alassa/scripts/d0alassa.baf~
        ~alassa/scripts/d0alas25.baf~
        ~alassa/scripts/d0alasd.baf~

COMPILE ~alassa/dialog~

COPY ~alassa/audio~ ~override~

COPY ~alassa/d0alassaaudio.bat~ ~d0alassaaudio.bat~

AT_INTERACTIVE_EXIT ~d0alassaaudio.bat~
AT_INTERACTIVE_UNINSTALL ~alassa/d0alassaaudiouninstall.bat~
